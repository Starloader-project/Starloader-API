plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'obftools' version '1.2.0'
}

group 'de.geolykt'
def archivesBaseName = 'starloader-api'
def gdxVersion = '1.9.11' // The InputAdapter class is otherwise invalid
version '1.5.0'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '11'

repositories {
    maven {
        name 'geolykt'
        url 'https://geolykt.de/maven'
    }
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven'
    }
    mavenCentral()
    mavenLocal()
}

obfTools {
    annotation = "Lde/geolykt/starloader/impl/Pseudo;"
    affectedJar = "./galimulator-desktop.jar"
    accessWidener = "src/main/resources/starloader-api.accesswidener"
}

dependencies {
    // Galimulator proper
    compileOnly files("build/obftools/intermediary.jar")
    api "de.geolykt:starloader:1.1.0-SNAPSHOT" // The minimalistic starloader base "api" as well all of it's depends

    // All APIs that are already provided by galimulator (natives excluded)
    // Note: these libraries are likely to be too updated as stuff like LWJGL 2 is still used internally.
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    api "xmlpull:xmlpull:1.1.3.4d_b4_min"
    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    api group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.16'

    testImplementation 'junit:junit:4.12'
}

javadoc {
    options {
        addBooleanOption('html5', true)
        tags("apiNote:a:API Notice:", "implSpec:a:Implementation Specification:", "implNote:a:Implementation Notice:")
        links "https://docs.oracle.com/en/java/javase/16/docs/api/"
    }

    // see https://stackoverflow.com/a/56641766
    doLast {
        // Append the fix to the file
        def searchScript = new File(destinationDir, '/search.js')
        searchScript.append '\n\n' +
            'getURLPrefix = function(ui) {\n' +
            '    return \'\';\n' +
            '};\n'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build {
    dependsOn postprocess
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version '1.5.0'

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
    }
}
