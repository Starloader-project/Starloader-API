plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'obftools' version '1.4.0'
}

group 'de.geolykt'
def archivesBaseName = 'starloader-api'
def gdxVersion = '1.9.11' // The InputAdapter class is otherwise invalid
version '1.6.0'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '11'

repositories {
    maven {
        name 'geolykt'
        url 'https://geolykt.de/maven'
    }
    mavenCentral()
    mavenLocal()
}

obfTools {
    annotation = "Lde/geolykt/starloader/impl/Pseudo;"
    affectedJar = "./galimulator-desktop.jar"
    accessWidener = "src/main/resources/starloader-api.accesswidener"
}

dependencies {
    // Galimulator proper
    compileOnly files("build/obftools/intermediary.jar")
    compileOnlyApi ("de.geolykt:starloader:1.1.0-SNAPSHOT") { // The minimalistic starloader base "api" as well all of it's depends
        // SLL 3.+ does not ship with log4j as it does not have proper JPMS support
        // Additionally, log4j is vulnerable to a critical security issue
        exclude module: 'log4j-api'
        exclude module: 'log4j-core'
        exclude module: 'log4j-slf4j-impl'
        exclude module: 'mixin' // Does not want to resolve correctly
    }
    // We removed these depends above
    compileOnlyApi "org.slf4j:slf4j-api:2.0.0-alpha5"
    compileOnlyApi "de.geolykt.starloader:mixin:0.8.4"

    // All APIs that are already provided by galimulator (natives excluded)
    // Note: these libraries are likely to be too updated as stuff like LWJGL 2 is still used internally.
    compileOnlyApi "com.badlogicgames.gdx:gdx:$gdxVersion"
    compileOnlyApi "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compileOnlyApi "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compileOnlyApi "xmlpull:xmlpull:1.1.3.4d_b4_min"
    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    compileOnlyApi group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.16'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.ow2.asm:asm-util:9.2'
}

javadoc {
    options {
        addBooleanOption('html5', true)
        tags("apiNote:a:API Notice:", "implSpec:a:Implementation Specification:", "implNote:a:Implementation Notice:")
        links "https://docs.oracle.com/en/java/javase/16/docs/api/"
    }

    // see https://stackoverflow.com/a/56641766
    doLast {
        // Append the fix to the file
        def searchScript = new File(destinationDir, '/search.js')
        searchScript.append '\n\n' +
            'getURLPrefix = function(ui) {\n' +
            '    return \'\';\n' +
            '};\n'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build {
    dependsOn postprocess
}

publish {
    dependsOn postprocess
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version '1.5.0'

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
        /*maven {
            url = "/home/Geolykt/maven-alt/repo/"
        }*/
    }
}
